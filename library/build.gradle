apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven'

def auraVersionCode = (int) (new Date().getTime() / 300000)
def auraVersionName = '1.0.' + String.valueOf(auraVersionCode)

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode auraVersionCode
        versionName auraVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                jacoco {
                    includeNoLocationClasses = true
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    maven{url = "http://172.104.79.181:81/repository/Elizabeth/"}
}

dependencies {
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha3')
    implementation('com.squareup.picasso:picasso:2.71828') {
        // find compatible one to remove exclude
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'exifinterface'
    }
    implementation 'androidx.fragment:fragment:1.0.0-rc01'
    implementation 'androidx.appcompat:appcompat:1.0.0-rc01'
    implementation 'com.google.android.material:material:1.0.0-rc01'
    implementation 'androidx.recyclerview:recyclerview:1.0.0-rc01'
    implementation 'com.silverhetch:clotho:1.0.0-5105197'
    implementation 'com.google.code.gson:gson:2.8.5'
    testImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}


apply plugin: 'jacoco'
jacoco {
    toolVersion = "0.7.9"
}

project.afterEvaluate {
    android.libraryVariants.all { variant ->
        def name = variant.name
        def testTaskName = "test${name.capitalize()}UnitTest"

        tasks.create(name: "${testTaskName}Coverage", type: JacocoReport, dependsOn: "$testTaskName") {
            group = "Reporting"
            description = "Generate Jacoco coverage reports for the ${name.capitalize()} build."

            classDirectories = fileTree(
                    dir: "${project.buildDir}/intermediates/javac/$name/",
                    includes: ['**/com/silverhetch/aura/**'],
                    excludes: ['**/R.class',
                               '**/R$*.class',
                               '**/*$ViewInjector*.*',
                               '**/*$ViewBinder*.*',
                               '**/BuildConfig.*',
                               '**/Manifest*.*']
            )+fileTree(
                    dir: "${project.buildDir}/tmp/kotlin-classes/$name",
                    includes: ['**/com/silverhetch/aura/**'],
                    excludes: ['**/R.class',
                               '**/R$*.class',
                               '**/*$ViewInjector*.*',
                               '**/*$ViewBinder*.*',
                               '**/BuildConfig.*',
                               '**/Manifest*.*']
            )

            sourceDirectories = files(["${project.projectDir}/src/main/java","${project.projectDir}/src/${variant.flavorName}/java"].plus(android.sourceSets[name].java.srcDirs))
            executionData = files("${project.buildDir}/jacoco/${testTaskName}.exec")

            reports {
                xml.enabled = true
                html.enabled = true
            }
        }
    }
}


apply plugin: 'com.jfrog.bintray'
bintray {
    user = 'larryhsiao'
    key = System.getenv('BINTRAY_KEY')
    publications = ['release']
    publish = true
    override = true
    pkg {
        repo = 'Elizabeth'
        name = 'Aura'
        userOrg = 'larryhsiao'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/LarryHsiao/Aura.git'
        version {
            name = auraVersionName
            gpg{
                sign = true
                passphrase = System.getenv('BINTRAY_PASSPHRASE')
            }
        }
    }
}

apply plugin: 'digital.wup.android-maven-publish'
publishing {
    publications {
        release(MavenPublication) {
            from components.android
            groupId 'com.silverhetch'
            artifactId 'aura'
            version auraVersionName
        }
    }
}
